apply plugin: 'com.android.application'
apply plugin: 'retrolambda'
apply plugin: 'de.undercouch.download'
apply plugin: 'imagemagick'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    defaultConfig {
        applicationId "at.yawk.fiction.android"
        minSdkVersion 15
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile files('../dep/build/libs/dep-all.jar')
    provided 'org.projectlombok:lombok:1.16.4'
    provided 'com.google.code.findbugs:annotations:2.0.2'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'
}

task resizeResources(type: com.eowise.imagemagick.tasks.Magick) {
    convert projectDir.toString() + "/tmp", { include '*' }
    into 'src/main/res/drawable'
    actions {
        inputFile()
        -sample('500x500')
        outputFile()
    }
}

task downloadResources << {
    download {
        src 'https://www.fanfiction.net/static/images/favicon_2010_site.png'
        dest new File(projectDir, 'tmp/icon.png')
    }
    tasks.resizeResources.execute()
    new File(projectDir, 'tmp').deleteDir()
}