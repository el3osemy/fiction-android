apply plugin: 'com.android.application'
apply plugin: 'retrolambda'
apply plugin: 'de.undercouch.download'
apply plugin: 'imagemagick'
apply plugin: 'com.tmiyamon.mdicons'

private String getenv(String name, String defaultValue) {
    String value = System.getenv(name);
    return value == null ? defaultValue : value;
}

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    signingConfigs {
        release {
            storeFile file(getenv("KEYSTORE_FILE", "/missing"))
            storePassword getenv("KEYSTORE_PASSWORD", "")
            keyAlias getenv("KEY_NAME", "")
            keyPassword getenv("KEY_PASSWORD", "")
        }
    }
    defaultConfig {
        applicationId "at.yawk.fiction.android"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        multiDexEnabled = true
    }
    productFlavors {
        'public' {

        }
        'private' {

        }
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/DEPENDENCIES'
    }
    lintOptions {
        disable 'InvalidPackage'
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile files('../dep/build/libs/dep-all.jar')
    provided 'org.projectlombok:lombok:1.16.4'
    provided 'com.google.code.findbugs:annotations:2.0.2'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
    compile 'org.apmem.tools:layouts:1.9@aar'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.j256.ormlite:ormlite-android:4.48'
    compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'org.flywaydb:flyway-core:3.2.1'

    testCompile 'org.testng:testng:6.9.6'
}

mdicons {
    defcolor "white", "#ffffff"

    assets {
        image {
            colors "white"
            sizes "24dp"
            names "edit"
        }
        nav {
            colors "white"
            sizes "24dp"
            names "refresh", "cancel", "menu"
        }
        file {
            colors "white"
            sizes "24dp"
            names "file_download"
        }
        content {
            colors "white"
            sizes "24dp"
            names "add"
        }
        action {
            colors "white"
            sizes "24dp"
            names "settings"
        }
        hardware {
            colors "white"
            sizes "24dp"
            names "keyboard_arrow_up", "keyboard_arrow_down"
        }
    }
}

task resizeResources(type: com.eowise.imagemagick.tasks.Magick) {
    convert projectDir.toString() + "/tmp", { include '*' }
    into 'src/main/res/drawable'
    actions {
        inputFile()
        -sample('500x500')
        outputFile()
    }
}

task downloadResources << {
    download {
        src 'https://www.fanfiction.net/static/images/favicon_2010_site.png'
        dest new File(projectDir, 'tmp/icon.png')
    }
    tasks.resizeResources.execute()
    new File(projectDir, 'tmp').deleteDir()
}